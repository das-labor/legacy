; ------------------------------------------------
; Fehlerbehandlung FPGA Komunikation 04/2009 SAURON
; ------------------------------------------------

; Bei der Assemblierung wird ein ROM Baustein mit JTAG Anschluss erstellt
                    VHDL      "jtag_template.vhd", "d:\\borg_sender\sender\picoblaze_rom.vhd", "pb_rom"
                    MEM       "picoblaze.mem"     ; Assembliertes Programm exportieren
                    EXEC      "transfer.bat"      ; und übertragen an den FPGA
; Portmap für den Simulator

; Eingangs - Ports

; Anbindung an Daten_Schieber
mem_datain1         DSIN      0
mem_datain2         DSIN      1
mem_datain3         DSIN      2
mem_datain4         DSIN      3
mem_wr_en1          DSOUT     60
mem_wr_en2          DSOUT     61
mem_wr_en3          DSOUT     62
mem_wr_en4          DSOUT     63
mem_dataout1        DSOUT     64
mem_dataout2        DSOUT     65
mem_dataout3        DSOUT     66
mem_dataout4        DSOUT     67
mem_addr1           DSOUT     68
mem_addr2           DSOUT     69
mem_addr3           DSOUT     70
mem_addr4           DSOUT     71

; Anbindung an tpg
tpg_pattern         DSOUT     13
tpg_mux             DSOUT     14
tpg_i1              DSOUT     15
tpg_i2              DSOUT     16
tpg_i3              DSOUT     17
tpg_i4              DSOUT     18
q9                  DSOUT     19

; Anbindung an 7-Segment Display
int_hi              DSOUT     24                  ; segment encoding
int_lo              DSOUT     25                  ; .     0
bin0                DSOUT     26                  ; .    ---
bin1                DSOUT     27                  ; . 5 |   | 1
bin2                DSOUT     28                  ; .    ---   <- 6
bin3                DSOUT     29                  ; . 4 |   | 2
hell0               DSOUT     30                  ; .    ---  *
hell1               DSOUT     31                  ; .     3   7
hell2               DSOUT     32
hell3               DSOUT     33
hex_mode            DSOUT     34
bin_mode            DSOUT     35
punkte              DSOUT     36
; kleinkram
led                 DSOUT     40                  ; LEDS
schalter            DSIN      50                  ; Schalter und Taster
Taster              DSIN      51


                    CALL      init

; ========================== MENÜ ==========================================
; Taste  1 = Speichermanipulation
; Taster 2 = Testpattern für de-jitterung auf allen Kanälen
; Taster 3 = Daten Ausgeben
; Taster 4 = noch frei

; s9 Taster
; sf dauer Taster gedrückt

menu:               LOAD      s0, 1               ; Display Umschalten auf
                    OUT       s0, bin_mode        ; Bin Mode
                    OUT       s0, punkte
                    LOAD      s4, %01001001       ; Binaermuster
                    OUT       s4, bin0            ; auf
                    OUT       s4, bin1            ; allen
                    OUT       s4, bin2            ; stellen
                    OUT       s4, bin3            ; ausgeben
menuloop:           CALL      debounce            ; Taster einlesen
                    COMP      sF, 1               ; Taster kurz gedrückt ?
                    JUMP      NZ, menuloop        ; nein ? : ignorieren
                    COMP      s9, %00001000       ; Taster BTN3 gedrückt ?
                    JUMP      Z, memloop          ; ja: zur memory-bearbeitung
                    COMP      s9, %00000100       ; Taster btn2 gedrückt ?
                    JUMP      Z, testpattern      ; ja: zur Testpattern generierung
                    COMP      s9, %00000010       ; Taster BTN1 gedrückt ?
                    JUMP      Z, dataout          ; ja: zur datengenerierung
                    JUMP      menuloop

; ======================== Daten Ausgeben ====================================
; Kanal mit den Linken Tasten auswählen,
; Auszugebende Daten mit den Schaltern einstellen
; Daten = 0   :  Kanal aus
; Daten = 255 :  zufällige Daten ausgeben

; sc - Kanalnummer

dataout:            LOAD      s0, 0
                    OUT       s0, bin_mode
                    OUT       s0, tpg_mux         ; TPG muxer auf Datenausgabe

dataloop:           CALL      debounce            ; Taster einlesen
                    LOAD      s0, s9
                    XOR       s0, %00001000
                    CALL      Z, kanalplus        ; Taster BTN3 gedrückt ?

                    LOAD      s0, s9
                    XOR       s0, %00000100       ; Taster BTN2 gedrückt ?
                    CALL      Z, kanalminus

                    LOAD      s0, s9
                    XOR       s0, %00001000       ; Taster BTN3 gedrückt ?
                    JUMP      NZ, dataloop3
                    COMP      sF, 250             ; Taster lange gedrückt ?
                    JUMP      NZ, dataloop3
                    JUMP      menu                ; dann zum Menu

dataloop3:          LOAD      s0, 10              ; sc mit
                    LOAD      s1, sC              ; 10 Multiplizieren
                    CALL      multiplier
                    LOAD      s0, 100             ; und nochmal
                    LOAD      s1, s4              ; mit 100
                    CALL      multiplier          ; multiplizieren

                    IN        sA, schalter        ; schalter einlesen
                    ADD       s4, sA              ; und zu den x.000
                    ADDC      s3, 0               ; dazu addieren

                    OUT       s3, int_hi          ; Ausgeben auf
                    OUT       s4, int_lo          ; display
                    LOAD      s0, %000001000      ; komma auf
                    OUT       s0, punkte          ; erste stelle setzen

                    COMP      sC, 0               ; wenn Kanal = 0
                    JUMP      NZ, dl1             ; dann ausgeben auf tpg_i1
                    OUT       sA, tpg_i1
dl1:                COMP      sC, 1               ; wenn Kanal = 1
                    JUMP      NZ, dl2             ; dann ausgeben auf tpg_i2
                    OUT       sA, tpg_i2
dl2:                COMP      sC, 2               ; wenn Kanal = 2
                    JUMP      NZ, dl3             ; dann ausgeben auf tpg_i3
                    OUT       sA, tpg_i3
dl3:                COMP      sC, 3               ; wenn Kanal = 3
                    JUMP      NZ, dl4             ; dann ausgeben auf tpg_i4
                    OUT       sA, tpg_i4
dl4:                JUMP      dataloop

; ============================= UNTERPROGRAMME Daten Ausgeben ===========================
kanalplus:          COMP      sF, 1               ; Taster schon länger gedrückt ?
                    JUMP      NZ, return2         ; Wenn ja dann abbruch
                    ADD       sC, 1               ; Anzahl sa schritte nach vorne springen
                    COMP      sC, 4               ; Überlauf ?
                    JUMP      NZ, return2         ; dannn wieder
                    LOAD      sC, 0               ; bei null anfangen
return2:            RET       

kanalminus:         COMP      sF, 1               ; Taster schon länger gedrückt ?
                    JUMP      NZ, return2         ; Wenn ja dann abbruch
                    SUB       sC, 1               ; Anzahl sa schritte nach hinten springen
                    COMP      sC, $FF             ; Unterlauf ?
                    JUMP      NZ, return2         ; dann wieder
                    LOAD      sC, 3               ; bei drei anfangen
                    RET       



; ======================== TESTPATTERN ====================================
; Testpattern auf allen Kanälen ausgeben
; Jeweils 2 Schalter bestimmen die Flanke auf einem Kanal

testpattern:        LOAD      s0, 0
                    OUT       s0, int_hi
                    OUT       s0, bin_mode
                    LOAD      s0, %00001111       ; Muxer auf
                    OUT       s0, tpg_mux         ; TPG schalten
testpatternloop:    IN        s0, schalter        ; Schalter auf
                    OUT       s0, tpg_pattern     ; TPG schalten
                    OUT       s0, int_lo
                    CALL      debounce
                    LOAD      s0, s9
                    XOR       s0, %00001000       ; Taster BTN3 gedrückt ?
                    JUMP      NZ, testpattern2
                    COMP      sF, 250             ; Taster lange gedrückt ?
                    JUMP      NZ, testpattern2
                    JUMP      menu                ; dann zum Menu
testpattern2:       JUMP      testpatternloop

; ========================== MEMORY ========================================
; Einzelne Werte im Speicher Manuell ändern und Anzeigen

; s8 - Speicher Addresse
; s9 - Taster
; sa - Schalter
; sb - Inhalt speicher
; sc - Kanalnummer
; sf - dauer Taster gedrückt


memloop: 
                    LOAD      s0, 0
                    OUT       s0, bin_mode
                    IN        sA, schalter        ; ändern um sa Schritte
                    COMP      sA, 0               ; sa=0 ? dann
                    JUMP      NZ, memloop2        ; sa=1
                    LOAD      sA, 1

memloop2:           CALL      debounce            ; Taster einlesen
                    LOAD      s0, s9
                    XOR       s0, %00001000
                    CALL      Z, memplus          ; Taster BTN3 gedrückt

                    LOAD      s0, s9
                    XOR       s0, %00000100       ; Taster BTN2 gedrückt
                    CALL      Z, memminus

                    LOAD      s0, s9
                    XOR       s0, %00000010       ; Taster BTN1 gedrückt
                    CALL      Z, meminc

                    LOAD      s0, s9
                    XOR       s0, %00000001       ; Taster BTN0 gedrückt
                    CALL      Z, memdec

                    LOAD      s0, s9
                    XOR       s0, %00001000       ; Taster BTN3 gedrückt ?
                    JUMP      NZ, memloop3
                    COMP      sF, 250             ; Taster lange gedrückt ?
                    JUMP      NZ, memloop3
                    JUMP      menu                ; dann zum Menu
memloop3: 
                    LOAD      s0, s9
                    XOR       s0, %100            ; Taster BTN2 gedrückt ?
                    JUMP      NZ, memloop4
                    COMP      sF, 250             ; Taster Lange gedrückt ?
                    JUMP      NZ, memloop4
                    ADD       sC, 1               ; KAnalnummer um 1 erhöhen
                    COMP      sC, 4
                    JUMP      NZ, memloop4
                    LOAD      sC, 0               ; Kanalnummer überlauf abfangen
memloop4: 
; LEDS bearbeiten
                    LOAD      s0, sC
                    LOAD      s1, %10000000
memloop4b:          COMP      s0, 0
                    JUMP      Z, memloop5
                    SUB       s0, 1
                    RR        s1
                    JUMP      memloop4b
memloop5:           OUT       s1, led

; Display bearbeitung
                    CALL      readmem             ; Speicheradresse Einlesen
                    CALL      malzehn             ; Addresse.inhalt
                    OUT       s2, int_lo
                    OUT       s1, int_hi
                    LOAD      s0, %00000010
                    OUT       s0, punkte          ; Einen Punkt Anzeigen
                    JUMP      memloop

; ============================= UNTERPROGRAMME MEMORY ======================================
memplus:            COMP      sF, 1               ; Taster schon länger gedrückt ?
                    JUMP      NZ, return          ; Wenn ja dann abbruch
                    ADD       s8, sA              ; Anzahl sa schritte nach vorne springen
return:             RET       

memminus:           COMP      sF, 1               ; Taster schon länger gedrückt ?
                    JUMP      NZ, return          ; Wenn ja dann abbruch
                    SUB       s8, sA              ; Anzahl sa schritte nach hinten springen
                    RET       

meminc:             COMP      sF, 1               ; Taster schon länger gedrückt ?
                    JUMP      NZ, return          ; Wenn ja dann abbruch
                    CALL      readmem
                    ADD       sB, 1               ; Memoryinhalt im 1 erhöhen
                    CALL      writemem
                    RET       

memdec:             COMP      sF, 1               ; Taster schon länger gedrückt ?
                    JUMP      NZ, return          ; Wenn ja dann abbruch
                    CALL      readmem
                    SUB       sB, 1               ; Memoryinhalt um 1 verringern
                    CALL      writemem
                    RET       

readmem:                                          ; Daten aus Block Ram Lesen
                    COMP      sC, 0               ; Bearbeitung Kanal 1
                    JUMP      NZ, readmem1
                    OUT       s8, mem_addr1       ; Zugriff auf speicheradresse s8
                    IN        sB, mem_datain1     ; inhalt nach sb kopieren
                    JUMP      readmemc
readmem1:           COMP      sC, 1               ; Bearbeitung Kanal 2
                    JUMP      NZ, readmem2
                    OUT       s8, mem_addr2       ; Zugriff auf speicheradresse s8
                    IN        sB, mem_datain2     ; inhalt nach sb kopieren
                    JUMP      readmemc
readmem2:           COMP      sC, 2               ; Bearbeitung Kanal 3
                    JUMP      NZ, readmem3
                    OUT       s8, mem_addr3       ; Zugriff auf speicheradresse s8
                    IN        sB, mem_datain3     ; inhalt nach sb kopieren
                    JUMP      readmemc
readmem3:           COMP      sC, 3               ; Bearbeitung Kanal 4
                    JUMP      NZ, readmemc
                    OUT       s8, mem_addr4       ; Zugriff auf speicheradresse s8
                    IN        sB, mem_datain4     ; inhalt nach sb kopieren
                    JUMP      readmemc
readmemc:           RET       

writemem:                                         ; Daten in Block Ram schreiben
                    COMP      sC, 0
                    JUMP      NZ, writemem1
                    OUT       s8, mem_addr1       ; Zugriff auf Speicheradresse s8
                    OUT       sB, mem_dataout1    ; Inhalt von sb ausgeben
                    LOAD      s0, 1               ; Kurzer impuls
                    OUT       s0, mem_wr_en1      ; auf Mem_wr_en
                    LOAD      s0, 0               ; sorgt für
                    OUT       s0, mem_wr_en1      ; das Speichern
                    JUMP      writememc
writemem1:          COMP      sC, 1
                    JUMP      NZ, writemem2
                    OUT       s8, mem_addr2       ; Zugriff auf Speicheradresse s8
                    OUT       sB, mem_dataout2    ; Inhalt von sb ausgeben
                    LOAD      s0, 1               ; Kurzer impuls
                    OUT       s0, mem_wr_en2      ; auf Mem_wr_en
                    LOAD      s0, 0               ; sorgt für
                    OUT       s0, mem_wr_en2      ; das Speichern
                    JUMP      writememc
writemem2: 
                    COMP      sC, 2
                    JUMP      NZ, writemem3
                    OUT       s8, mem_addr3       ; Zugriff auf Speicheradresse s8
                    OUT       sB, mem_dataout3    ; Inhalt von sb ausgeben
                    LOAD      s0, 1               ; Kurzer impuls
                    OUT       s0, mem_wr_en3      ; auf Mem_wr_en
                    LOAD      s0, 0               ; sorgt für
                    OUT       s0, mem_wr_en3      ; das Speichern
                    JUMP      writememc
writemem3: 
                    COMP      sC, 3
                    JUMP      NZ, writememc
                    OUT       s8, mem_addr4       ; Zugriff auf Speicheradresse s8
                    OUT       sB, mem_dataout4    ; Inhalt von sb ausgeben
                    LOAD      s0, 1               ; Kurzer impuls
                    OUT       s0, mem_wr_en4      ; auf Mem_wr_en
                    LOAD      s0, 0               ; sorgt für
                    OUT       s0, mem_wr_en4      ; das Speichern
                    JUMP      writememc

writememc:          RET       

malzehn:            LOAD      s0, 10              ; schleifenzähler
                    LOAD      s1, 0               ; Ergebnis High Byte
                    LOAD      s2, 0               ; Ergebnis Low Byte
malzehn2:           ADD       s2, s8              ; Speicheraddresse Addieren
                    ADDC      s1, 0               ; Überlauf beachten
                    SUB       s0, 1               ; Schleifenzähler incrementieren
                    JUMP      NZ, malzehn2        ; noch nicht 10 mal?
                    ADD       s2, sB              ; Speicherinhalt addieren
                    ADDC      s1, 0               ; Überlauf beachten
                    RET       

; =============================  ALLGEMEINE UNTERPROGRAMME =================================
; -             --- Unterprogramm Entprellen ---
debounce:           LOAD      s4, 100             ; Anzahl durchläufe
debounce1:          IN        s2, taster          ; Taster einlesen,
                    LOAD      s0, 0
                    LOAD      s1, 2               ; kurz warten
                    CALL      wait                ; nochmal einlesen
                    IN        s3, taster
                    COMP      s2, s3              ; Hat sich was geändert ?
                    JUMP      NZ, debounceexit    ; Signal nicht stabil : Abbruch !
                    SUB       s4, 1               ; und noch einmal
                    JUMP      NZ, debounce1       ; das selbe
                    LOAD      s9, s3              ; Signal ist stabil: übernahme in s9
                    COMP      s9, 0
                    JUMP      NZ, debounce2       ; wenn keine Taste gedrückt
                    LOAD      sF, 0               ; zähler für dauer auf 0
                    JUMP      debounceexit
debounce2:          COMP      sF, 255             ; Zähler schon auf maximum ?
                    JUMP      Z, debounceexit     ; wenn ja, abbruch
                    ADD       sF, 1               ; Zähler increment
debounceexit:       RET       

; -             --- Unterprogramm Warten ---
wait_short:         LOAD      s1, 255             ; Einsprungsadresse ohne Wartezeit vorgabe
                    LOAD      s0, 255
wait:               LOAD      s0, s0              ; NOP
                    SUB       s0, 1               ; In s0 (low) und s1 (high) muss die
                    JUMP      NZ, wait            ; Wartezeit übergeben werden
                    SUB       s1, 1
                    JUMP      NZ, wait
                    RET       

; -             --- 8 X 8 Multiplizierer ---
multiplier:         LOAD      s2, 1               ; s0 - multiplicand
                    LOAD      s3, 0               ; s1 - multiplier
                    LOAD      s4, 0               ; s3 - result msb
mult_loop:          TEST      s1, s2              ; s4 - result lsb
                    JUMP      Z, no_add
                    ADD       s3, s0
no_add:             SRA       s3
                    SRA       s4
                    SL0       s2
                    JUMP      NZ, mult_loop
                    RET       

; -             --- Initialisierung ---
; Nach einem Reset werden die Ausgangs - Ports auf 0 gesetzt
; Die Block rams bleiben unverändert

init:               LOAD      s8, 0
                    LOAD      s9, 0
                    LOAD      sA, 0
                    LOAD      sB, 0
                    LOAD      sC, 0
                    LOAD      sD, 0
                    LOAD      sE, 0
                    LOAD      sF, 0
                    LOAD      s0, 13
init2:              OUT       s8, s0
                    ADD       s0, 1
                    COMP      s0, 20
                    JUMP      NZ, init2
                    LOAD      s0, 24
init3:              OUT       s8, s0
                    ADD       s0, 1
                    COMP      s0, 37
                    JUMP      NZ, init3
                    LOAD      s0, 60
init4:              OUT       s8, s0
                    ADD       s0, 1
                    COMP      s0, 72
                    JUMP      NZ, init4
                    OUT       s8, led
                    LOAD      s0, 255
                    OUT       s0, hell0
                    OUT       s0, hell1
                    OUT       s0, hell2
                    OUT       s0, hell3
                    RET       
